use strict;

=head1 NAME

summit

=head1 DESCRIPTION

This plugin handles replies to basecamp emails

=head1 CONFIG

Add this line before rcpt_ok and then test

summit db_name $db_name db_user $db_user db_host $db_host db_pass $db_pass

=head1 BUGS

=head1 AUTHOR

Written by Fred Moyer <fred@redhotpenguin.com>.

=head1 COPYRIGHT

Copyright 2006 Red Hot Penguin Consulting LLC

=cut

use lib "/home/fred/dev/summit/trunk/lib";

use Data::Dumper;
use DBI;
use Summit::DB;

use constant SHERPA_OK => 200;
use constant SHERPA_COMMENT_ERROR => 500;
use constant SHERPA_NO_URL_FOUND => 404;
use constant SHERPA_NO_COMMENT_FOUND => 406;


my $VERSION = '0.01';

sub init {
    my ($self, $qp, %args) = @_;

    my $params_ref = Summit::DB->params(%args);
    $self->{dbh_connect} = $params_ref;
}

sub hook_rcpt {
    my ($self, $transaction, $recipient) = @_;

    my $sender = $transaction->sender->address;
    $self->log(LOGDEBUG, "Recipient is " . $recipient->address);
    $self->log(LOGDEBUG, "sender is $sender");
    my ($basecamp_login, $basecamp_pass, $account_id) =
      $self->_authenticate($sender, $recipient->address,);

    if ($basecamp_login && $basecamp_pass) {
        $self->log(LOGDEBUG, "Summit user found");
        $self->register_hook('data_post', 'summit_handler');
        $self->{'basecamp_login'} = $basecamp_login;
        $self->{'basecamp_pass'}  = $basecamp_pass;
		$self->{'account_id'}	  = $account_id;
        return OK;
    }
    $self->log(LOGINFO, "$sender is NOT A Summit user");
    return DECLINED;
}

sub _authenticate {
    my ($self, $sender, $recipient) = @_;

    my $dbh = DBI->connect(@{$self->{dbh_connect}})
      or die print STDERR "Could not establish db connection";
    my $sql = <<SQL;
SELECT basecamp_login, basecamp_pass, account_id from account
WHERE sender = ? AND recipient = ? AND active = 't'
SQL

    my $sth = $dbh->prepare_cached($sql);
    $sth->bind_param(1, $sender);
    $sth->bind_param(2, $recipient);
    my $rv = $sth->execute;
    $self->log(LOGDEBUG, "authentication query, params $sender, $recipient");
    unless ($rv) {
        $self->log(LOGERROR, "Failed to execute $sql, err $DBI::errstr");
        return;
    }

    my $ary_ref = $sth->fetchrow_arrayref;
    $self->log(LOGDEBUG, "Query results: " . Dumper($ary_ref));

    $dbh->rollback;

	$self->{dbh} = $dbh;
    if ($ary_ref->[0]) {
        return (@{$ary_ref});
    }
    return;
}

sub _get_body {
    my $transaction = shift;
    $transaction->body_resetpos;
    my $body;
    while (my $line = $transaction->body_getline) {
        $body .= $line;
    }
    return $body;
}

sub summit_handler {
    my ($self, $transaction) = @_;

    $DB::single = 1;
    my @recipients = $transaction->recipients;
    my $recipient  = $recipients[0]->address;
    my $sender     = $transaction->sender->address;
    $self->log(LOGDEBUG, "recipient is " . Dumper($recipient));

    my $body = _get_body($transaction);

    #	$self->log(LOGDEBUG, "Transaction body" . $body);
    require Summit::Message;
    my $msg =
      Summit::Message->new(
                           {
                            header => $transaction->headers->as_string,
                            body   => $body
                           }
                          );
    $msg->basecamp_login($self->{_basecamp_login});
    $msg->basecamp_pass($self->{_basecamp_pass});

    return &$self->_no_url_found($transaction)     if !$msg->url;
    return &$self->_no_comment_found($transaction) if !$msg->comment;
    return &$self->_comment_error($transaction)    if !$msg->post_to_basecamp;
    return &$self->_send_receipt($transaction);
}

sub _log_transaction {
	my ($self, $code) = @_;

    my $dbh = $self->{dbh} || (DBI->connect(@{$self->{dbh_connect}})
      or die print STDERR "Could not establish db connection");

    my $sql = <<SQL;
INSERT INTO TRANSACTION (account_id, code) values (?, ?)
SQL
	my $sth = $dbh->prepare($sql);
    $sth->bind_param(1, $self->{'account_id'});
    $sth->bind_param(2, $code);
    my $rv = $sth->execute;
	$dbh->commit;
	defined $rv ? return 1 : return;
}

sub _no_comment_found {
    my ($self, $transaction) = @_;
	$self->_log_transaction(SHERPA_NO_COMMENT_FOUND);    
    $self->log(LOGERROR, "Hmm no post message found");
    my $msg = <<MSG;
\nWe couldn't find your comment.  Please make sure that it had at least two returns separating it from the original message\n\n
MSG
    $transaction->body_front_write($msg);
    $transaction->recipients($transaction->sender);
    return OK;
}

sub _send_receipt {
    my ($self, $transaction) = @_;

	$self->_log_transaction(SHERPA_OK);    
	$transaction->recipients($transaction->sender);
    my $response = "\n** Your response has been posted\n";
    $transaction->body_front_write($response);
    return OK;
}

sub _no_url_found {
    my ($self, $transaction) = @_;
	$self->_log_transaction(SHERPA_NO_URL_FOUND);    

    $self->log(LOGERROR, "Hmm no url found");
    my $msg = "\nSorry we couldn't find a basecamp url in your message\n";
    $transaction->body_front_write($msg);
    $transaction->recipients($transaction->sender);
    return OK;
}

sub _comment_error {
    my ($self, $transaction) = @_;
	$self->_log_transaction(SHERPA_COMMENT_ERROR);    
    $self->log(LOGERROR, $self->_comment_err_log);
    $transaction->body_front_write($self->_comment_err_msg);
    $transaction->recipients($transaction->sender);
    return OK;
}

